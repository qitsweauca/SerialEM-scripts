ScriptName MMMCars
# chen.xu@umassmed.edu 
# Last-Update: 2019-05-04 
# tested!

# make all the MMMs from different cartriges automatically!!!
# use property of current registration to do so. 

echo Run this from Acquire at Points... 


echo #############################################
echo ###                 ClearPersistentvars                           ####
echo #############################################

# find out this from "shift to marker". If the mags for LMM and V are not changed and scope is not 
# realigned, these two values should remain the same. Better off checn it. 

xShift = 9
yShift = -4

# !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!  No Editting belo1 !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! #

##########################
# navigator must be open
##########################
echo MMMCars!!!

# 2. Call Cars
Call Cars

# 3. make LMM appropriate map
SetProperty RealignItemMaxLMfield 700

## Define persistant variable 
IsVariableDefined regInd
If $repVal1 == 0 
     regInd := 1           
Endif

CallFunction MyFuncs::C2_150-OBJ_out
ReportNavItem 
mesh=$navLabel
SetNavRegistration $navRegis
LoadCartridge $navRegis
SetColumnOrGunValve 1
RealignToNavItem 0
ResetImageShift 
Copy A P
Search 
AlignTo P
ResetImageShift 
MoveStage $xShift $yShift

# 4. V, fix Z 
GoToLowDoseArea V
CallFunction MyFuncs::C2_30-OBJ_70
V
Copy A T
Call Z
UpdateGroupZ 
V
AlignTo T

# 5. MMM. No need for pre-setup template
SetUserSetting MontageUseViewInLD 1
SetUserSetting  MontageAlignPieces 1
SetUserSetting MontageVerySloppy 0
AllowFileOverwrite 1
OpenNewMontage 4 6  Car$navRegis-Mesh$mesh-MMM.map
SetMontageParams 1 576 410 5760 4092 1 1        
Montage 
NewMap 
CallFunction MyFuncs::NewMapToJPG
CloseFile 


# 6. CenterBeam & autofocus
AutoCenterBeam 
SetTargetDefocus -2.3
G

# Record single shot

## ReportNavItem -> NOT here, as it will report newly created map
AllowFileOverwrite 1
OpenNewFile Car$navRegis-Mesh$mesh-R.st
ReportStageXYZ baseX baseY
# int = 3. spacing = 4micron offset = 8.0, around middle (8.0 here)
# -> offset = ( spacing + int * spacing ) / 2 
# 4 x 4, 3, =>7.5
# 4 x 4, 4, => 10

n = 5
d = 4
offset = ( $d + $n * $d ) / 2 
Loop $n ix
    x = $baseX + $ix * $d - $offset
    Echo Doing column at X = $X
    Loop $n iy
         y = $baseY + $iy * $d - $offset
         MoveStageTo $x $y
         #Autofocus
          SetExposure R 1.997
          SetBinning R 1
          SetFrameTime R 0.1995
          SetDoseFracParams R 1 0 1 1 0
          Record
          Save
         #Montage 
         NewMap
         CallFunction MyFuncs::NewMapToJPG
    EndLoop
EndLoop
CloseFile 

# restore stage postion
MoveStageTo $baseX $baseY

## 8.  if last point of this registration, switch to next available registration
ReportNumNavAcquire 
If $repVal1 == 1  
     regInd := $regInd + 1
      If $regInd > $#cat
         regInd :=                            # clear the persistant variable
         exit
       Else 
           echo SetNavRegistration $cat[$regInd]
           SetNavRegistration $cat[$regInd]
           LoadCartridge $cat[$regInd]
       Endif   
 else 
       echo  +++++  Going to next item in the same Registration! +++++
Endif

# 9, make LMM inappropriate again 
SetProperty RealignItemMaxLMfield 5
